<div align="center" id="container"></div>
<script type="x-shader/x-vertex" id="vertexShader">
varying vec2 vUv;

void main() {
	vUv = uv;
	gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
}
</script>

<script type="x-shader/x-vertex" id="fragmentShader">
varying vec2 vUv;
uniform float u_time;
uniform float u_zoom ;
uniform float u_circles[32] ;

const float CIRCLE = 0.0 ;
const float LINE = 1.0 ;
const float END = 9.0 ;

void main() {
	float pi = 3.14159265359 ;
	float elevation = (vUv.y-0.5)*pi ;
	float azimuth = vUv.x*2.0*pi ;
	float Z = sin(elevation) ;
	float cosel = cos(elevation) ; 
	float X = cosel*cos(azimuth) ;
	float Y = cosel*sin(azimuth) ;
	vec2 coord = vec2(X/(1.-Z),Y/(1.-Z)) ;
	vec2 orbit = coord ;
	float factor = 1.0 ;
	const int maxFlips = 500;
	float flips = 0.0 ;

	for (int i=0; i < maxFlips; i+=1){
		int inside = 0 ;
		for (int j=0; j<32; j+=4){
			if (u_circles[j] == CIRCLE){
                float radius = u_circles[j+3] ;
				vec2 center = vec2(u_circles[j+1], u_circles[j+2]);
				vec2 diff = orbit-center ;
				float dist = length(diff) ;
				if (dist < radius){
					inside = 1 ;
				    float mult = radius * radius/(dist*dist) ;
					orbit = center + diff * mult ;
					factor *= mult ;
					flips += 1.0 ;
					break ;
				}
			}
			if (u_circles[j] == LINE){
				// reflection in a line
				// see http://www.sdmath.com/math/geometry/reflection_across_line.html#formulasabc for formula
				float A = u_circles[j+1] ;
				float B = u_circles[j+2] ;
				float C = u_circles[j+3] ;
                float x=orbit.x ;
				float y=orbit.y ;
				if (A*x+B*y+C > 0.0){
					inside = 1 ;
					float Asq = A*A ;
					float Bsq = B*B ;
                    orbit=vec2( ((Bsq-Asq)*x-2.*A*B*y-2.*A*C)/(Asq+Bsq),
								((Asq-Bsq)*y-2.*A*B*x-2.*B*C)/(Asq+Bsq)) ;
					flips += 1.0 ;
					break ;
				}
			}
			if (u_circles[j] == END){
				break ;
			}
		} // loop through circles and lines
		if (inside == 0){
			break ;
		}
	}

	float logval = log(factor/u_zoom/(1.0+dot(orbit,orbit)))/11.0 ;
	//float orbitLength = length(orbit) ;  
	logval = smoothstep (0.4, 0.6, logval) ;
	vec3 parityColor ;
	if (mod(flips,2.0)==0.0){
		parityColor = vec3(1.0,253./255.,55./255.) ;
	} 
    else {
		parityColor = vec3(253./255.,127./255.,35./255.) ;
	}
	vec3 logColor = vec3(logval) ;
	gl_FragColor = vec4(parityColor-logColor,1.) ;
}
</script>

<script src="scene.js"></script>
<script type="text/javascript"
    src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r99/three.min.js">
</script>

<script type="text/javascript"
    src="https://cdn.rawgit.com/mrdoob/three.js/r92/examples/js/controls/OrbitControls.js">
</script>
